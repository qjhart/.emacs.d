#+TITLE: Emacs configuration

This is my emacs configuration file, it's the strategy from the
https://github.com/hrs/dotfiles. Which is pretty terrific. I also got
inspiration from https://github.com/gjstein/emacs.d.  That's where I
decided to only add in the .emacs.d components in their seperate
repository.

The most crazy example of this might be
[[https://github.com/rougier/dotemacs/blob/master/dotemacs.org]]

Some other examples I've looked at, that are mostly for science-based
installations, include [[https://github.com/emacs-tw/awesome-emacs][Awesome Emacs]], and [[https://github.com/jkitchin/scimax][SciMax]].  [[https://prelude.emacsredux.com/en/latest/][Prelude]] is also worth a look
since it has a lot of very nice ideas on good packages to be using.

You should be able to clone this repo onto a new machine, and when you
start it up, your default emacs installation jsut comes up naturally
for you.  This is due, in part, to the use-package setup that allows
more auto-matic generation of your personal emacs setup.

This is setup by cloning, but remember to use `--recurse-submodules`
to get any submodules.  Otherwise, you'll have to `git submodule init;
git submodule update`.

#+BEGIN_SRC bash
git clone --recurse-submodules https://github.com/qjhart/.emacs.d.git
#+END_SRC

* Configuration
** Configure =use-package=
 I use =use-package= to install and configure my packages. My =init.el= includes
 the initial setup for =package.el= and ensures that =use-package= is installed,
 since I wanna do that right away.

 This makes sure that =use-package= will install the package if it's not already
 available. It also means that I should be able to open Emacs for the first time
 on a fresh Debian box and have my whole environment automatically installed. I'm
 not /totally/ sure about that, but we're gettin' close.

 #+begin_src emacs-lisp
   (require 'use-package-ensure)
   (setq use-package-always-ensure t)
 #+end_src

 Always compile packages, and use the newest version available.

 #+begin_src emacs-lisp
   (use-package auto-compile
     :config (auto-compile-on-load-mode))

   (setq load-prefer-newer t)
 #+end_src

 We want to be able to customize our minor modes using =:delight= in the
 =use-package= configurations.  This allows us to set minor visibility on here.

 #+BEGIN_SRC emacs-lisp
 (use-package delight)
 #+END_SRC

** Custom Auto-generated customizations
 I don't want any customizations stored in my ~init.el~ file, that just
 confuses things.  Instead store it seperately.

 #+BEGIN_SRC emacs-lisp
  (setq custom-file "~/.emacs.d/custom.el")
  (load custom-file :noerror)
 #+END_SRC


** Emacs Path
   Include .bashrc when executing shell commands.

   #+begin_src emacs-lisp
     (use-package exec-path-from-shell
     :ensure t
     :config (exec-path-from-shell-initialize))
   #+end_src

   #+RESULTS:
   : t

* Editor Keystrokes
** Command extensions
These are some various extensions that are commonly used.
*** =crux=
[[https://github.com/bbatsov/crux][Crux]] is a commonly used set of commands/keystrokes, used in prelude, eg.

 #+BEGIN_SRC emacs-lisp
(use-package crux)
 #+END_SRC

*** sensible defaults
 Lots of people have made some small initial modifications to some of the
 emacs defaults.  There are a lot, like better-defaults.el, but I like a few
 of the items in [[https://github.com/hrs/sensible-defaults.el.git][sensible-defaults]].  This is also a good way to show how you
 can use git submodules to include other peoples' packages that aren't in
 the emacs repo.  These are stored in the ~/.emacs.d/plugins]] location.
 However, we are using less and less of these entries, and pretty soon, we
 might be ready to move on without this.

 #+BEGIN_SRC emacs-lisp
	 (load-file "~/.emacs.d/plugins/sensible-defaults.el/sensible-defaults.el")
	 ;; (sensible-defaults/open-files-from-home-directory)
	 (sensible-defaults/increase-gc-threshold)
	 ;; (sensible-defaults/delete-trailing-whitespace)
	 ;; (sensible-defaults/treat-camelcase-as-separate-words)
	 (sensible-defaults/automatically-follow-symlinks)
	 (sensible-defaults/make-scripts-executable)
	 ;; (sensible-defaults/single-space-after-periods)
	 (sensible-defaults/offer-to-create-parent-directories-on-save)
	 (sensible-defaults/apply-changes-to-highlighted-region)
	 (sensible-defaults/overwrite-selected-text)
	 ;; (sensible-defaults/ensure-that-files-end-with-newline)
	 (sensible-defaults/confirm-closing-emacs)
	 (sensible-defaults/quiet-startup)
	 (sensible-defaults/make-dired-file-sizes-human-readable)
	 (sensible-defaults/shorten-yes-or-no)
	 (sensible-defaults/always-highlight-code)
	 (sensible-defaults/refresh-buffers-when-files-change)
	 (sensible-defaults/show-matching-parens)
	 (sensible-defaults/flash-screen-instead-of-ringing-bell)
	 (sensible-defaults/set-default-line-length-to 80)
	 ;; (sensible-defaults/open-clicked-files-in-same-frame-on-mac)
	 (sensible-defaults/yank-to-point-on-mouse-click)
	 ;; These are the keybindings
	 (sensible-defaults/bind-commenting-and-uncommenting)
	 ;; (sensible-defaults/bind-home-and-end-keys)
	 ;; (sensible-defaults/bind-keys-to-change-text-size)

	 ;; Save backups to /tmp
	 (sensible-defaults/backup-to-temp-directory)

	 ;; Double space users rule
	 (setq sentence-end-double-space t)
 #+END_SRC

*** Utility Functions
 These are used for more emacs customizations.
 #+BEGIN_SRC emacs-lisp
   (defun hrs/view-buffer-name ()
     "Display the filename of the current buffer."
     (interactive)
     (message (buffer-file-name)))

   (defun hrs/generate-scratch-buffer ()
     "Create and switch to a temporary scratch buffer with a random
        name."
     (interactive)
     (switch-to-buffer (make-temp-name "scratch-")))

   (defun hrs/de-unicode ()
     "Tidy up a buffer by replacing all special Unicode characters
        (smart quotes, etc.) with their more sane cousins"
     (interactive)
     (let ((unicode-map '(("[\u2018\|\u2019\|\u201A\|\uFFFD]" . "'")
                          ("[\u201c\|\u201d\|\u201e]" . "\"")
                          ("\u2013" . "--")
                          ("\u2014" . "---")
                          ("\u2026" . "...")
                          ("\u00A9" . "(c)")
                          ("\u00AE" . "(r)")
                          ("\u2122" . "TM")
                          ("[\u02DC\|\u00A0]" . " "))))
       (save-excursion
         (loop for (key . value) in unicode-map
               do
               (goto-char (point-min))
               (replace-regexp key value)))))

   (defun hrs/beautify-json ()
     "Pretty-print the JSON in the marked region. Currently shells
        out to `jsonpp'--be sure that's installed!"
     (interactive)
     (save-excursion
       (shell-command-on-region (mark) (point) "jsonpp" (buffer-name) t)))

   (defun hrs/unfill-paragraph ()
     "Takes a multi-line paragraph and makes it into a single line of text."
     (interactive)
     (let ((fill-column (point-max)))
       (fill-paragraph nil)))

   (defun hrs/kill-current-buffer ()
     "Kill the current buffer without prompting."
     (interactive)
     (kill-buffer (current-buffer)))

   (defun hrs/visit-last-dired-file ()
     "Open the last file in an open dired buffer."
     (end-of-buffer)
     (previous-line)
     (dired-find-file))

   (defun hrs/visit-last-migration ()
     "Open the last file in 'db/migrate/'. Relies on projectile. Pretty sloppy."
     (interactive)
     (dired (expand-file-name "db/migrate" (projectile-project-root)))
     (hrs/visit-last-dired-file)
     (kill-buffer "migrate"))

   (defun hrs/add-auto-mode (mode &rest patterns)
     "Add entries to `auto-mode-alist' to use `MODE' for all given file `PATTERNS'."
     (dolist (pattern patterns)
       (add-to-list 'auto-mode-alist (cons pattern mode))))

   (defun hrs/find-file-as-sudo ()
     (interactive)
     (let ((file-name (buffer-file-name)))
       (when file-name
         (find-alternate-file (concat "/sudo::" file-name)))))

 #+END_SRC
*** Better Custom
 These are just some additional customization flags, you can also find these in the Options area.
 #+BEGIN_SRC emacs-lisp
 (save-place-mode t)
 (show-paren-mode t)
 #+END_SRC

** Set Keys

Here are some of the common keybindings from =crux=

#+BEGIN_SRC emacs-lisp
;;(global-set-key (kbd "C-k") 'crux-smart-kill-line)
#+END_SRC

Assume that I always want to kill the current buffer when hitting =C-x k=.

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-x k") 'hrs/kill-current-buffer)
#+END_SRC

Use System style =C-x= =C-c= =C-v=.

#+BEGIN_SRC emacs-lisp
  (cua-mode t)
#+END_SRC

These are some of the default keystrokes from HRS.

#+begin_src emacs-lisp
  (define-key global-map "\C-cl" 'org-store-link)
  (define-key global-map "\C-ca" 'org-agenda)
  (define-key global-map "\C-cc" 'org-capture)
#+end_src

#+RESULTS:
: org-capture


** Editorconfig
It's better to use the editorconfig files to set your ending whitespace, tab
width, etc.  This allows you to play better with other editors or collaborators.

#+BEGIN_SRC emacs-lisp
(use-package editorconfig
  :ensure t
  :delight;;" .ec"
  :config
  (editorconfig-mode 1))
#+END_SRC

#+RESULTS:
: t

* Emacs Look and Feel
** Themes
There are many,many themes.  [[https://emacsthemes.com/][Emacs Themes]] is a good place to see alot of these.
If you want to interactively see how they affect your page, you can try ~M-x
custom-themes~

#+BEGIN_SRC emacs-lisp
   (use-package solarized-theme
     :ensure t)
#+END_SRC

#+RESULTS:

It's important to remember to fix your theme whenever you see a problem.  This
note on [[https://shallowsky.com/blog/linux/editors/emacs-customize-colors.html][Emacs Custom Colors]] is a good summary.  The basic idea is that when you
see a bad color, select it and use ~C-u C-x = ~

** Theme-Magic

Theme-Magic needs to have [[https://github.com/dylanaraps/pywal][pywal]] installed. The best way is to install this as a
user with ~pip3 install --user pywal~

#+begin_src emacs-lisp
  (use-package theme-magic
    :config
    (theme-magic-export-theme-mode)
    :delight theme-magic-export-theme-mode
    :ensure t)
#+end_src

#+RESULTS:
: t

[[https://github.com/bbatsov/solarized-emacs][Solarized Emacs]] theme allows you to use their setup, with any 8 color theme.
The order in solarized is:
darkest-base,brightest-base,yellow,orange,red,magenta,violet,blue,cyan,green.

These are converted in to .Xdefaults as:

| sol # | sol col        | .X #          | .X col      |
|-------+----------------+---------------+-------------|
|     0 | brightest-base | foreground    | black       |
|     1 | darkest-base   | N/A           |             |
|       | (fg+bg)/2      | background,15 |             |
|     2 | yellow         | 1,9           | red         |
|     3 | orange         | 4             | blue        |
|     4 | red            | 3,11          | green       |
|     5 | magenta        | 5,13          | magenta     |
|     6 | violet         | N/A           |             |
|     7 | blue           | 6,14          | cyan        |
|     8 | cyan           | 2,10          | yellow      |
|     9 | green          | 12            | bright blue |
|       | ~blend~        | 7             |             |
|       | ~blend~        | 8             |             |


#+begin_src emacs-lisp
  ;; inspired vim's jellybeans color-theme
  (solarized-create-theme-file-with-palette 'light 'solarized-jellybeans-light
    '("#202020" "#ffffff"
      "#ffb964" "#8fbfdc" "#a04040" "#b05080" "#805090" "#fad08a" "#99ad6a" "#8fbfdd"))

  (solarized-create-theme-file-with-palette 'dark 'solarized-jellybeans-dark
    '("#202020" "#ffffff"
      "#ffb964" "#8fbfdc" "#a04040" "#b05080" "#805090" "#fad08a" "#99ad6a" "#8fbfdd"))

  ;; https://marketingtoolbox.ucdavis.edu/brand-guide/colors
  (solarized-create-theme-file-with-palette 'light 'solarized-ucd-light
    '("#022851" "#FFF9E5"
      "#79242F" "#003A5D" "#266041" "#79242F" "#AADA91" "#008EAA" "#266041" "#F18A00")
    '((custom-theme-set-faces
       theme-name
       `(org-block-begin-line
         ((,class(:foreground ,(solarized-color-blend base03 base3 0.05 2)
                              :background ,(solarized-color-blend base03 base3 0.95 2)))))
       `(org-block
         ((,class (:background ,(solarized-color-blend base03 base3 0.95 2)))))
       `(org-block-end-line
         ((,class (:foreground ,(solarized-color-blend base03 base3 0.05 2)
                          :background ,(solarized-color-blend base03 base3 0.95 2)))))
       )
      `(mode-line
        ((,class (:foreground ,base2 :background ,(solarized-color-blend base03 base3 0.5 2)))))
      `(mode-line-inactive
        ((,class (:foreground ,base00 :background ,(solarized-color-blend base03 "black" 0.85 2)))))
      `(mode-line-buffer-id ((,class (:foreground ,base3 :weight bold))))
      )
  )

  (solarized-create-theme-file-with-palette 'dark 'solarized-ucd-dark
    '("#022851" "#FFF9E5"
      "#79242F" "#003A5D" "#266041" "#79242F" "#AADA91" "#008EAA" "#266041" "#F18A00")
        '((custom-theme-set-faces
       theme-name
       `(org-block-begin-line
         ((,class(:foreground ,(solarized-color-blend base03 base3 0.05 2)
                              :background ,(solarized-color-blend base03 base3 0.95 2)))))
       `(org-block
         ((,class (:background ,(solarized-color-blend base03 base3 0.95 2)))))
       `(org-block-end-line
         ((,class (:foreground ,(solarized-color-blend base03 base3 0.05 2)
                          :background ,(solarized-color-blend base03 base3 0.95 2)))))
       ))
    )
#+end_src

#+RESULTS:
: /home/quinn/.emacs.d/themes/solarized-ucd-dark.el

I like the idea of using a seperate color for source blocks.  There is a good
example of [[https://orgmode.org/worg/org-contrib/babel/examples/fontify-src-code-blocks.html][colored blocks]] for the lueven theme, which also discusses block
fontification.  There is an example of using the solarization theme builder, but
w/ custom faces in the wombat theme

#+RESULTS:
: /home/quinn/.emacs.d/themes/solarized-ucd-dark.el

** Menu Bars

The toolbar and scroll bar aren't super useful, but I can never remember all the
items in the menubar, so I leave that on.

#+BEGIN_SRC emacs-lisp
  (tool-bar-mode 0)
  (menu-bar-mode 1)
  (when window-system
    (scroll-bar-mode -1))
#+END_SRC

#+RESULTS:

** Set default font and configure font resizing

The standard =text-scale-= functions just resize the text in the current buffer;
I'd generally like to resize the text in /every/ buffer, and I usually want to
change the size of the modeline, too (this is especially helpful when
presenting). These functions and bindings let me resize everything all together!

Note that this overrides the default font-related keybindings from
=sensible-defaults=.

This sets the font to inconsolata, which usually isn't available on a fresh
install.  You can either comment that out, or install that font.  On debian
machines that is the ~fonts-inconsolata~ package.

#+BEGIN_SRC emacs-lisp
  (setq hrs/default-font "monospace")
  (setq hrs/default-font-size 18)
  (setq hrs/current-font-size hrs/default-font-size)

  (setq hrs/font-change-increment 1.1)

  (defun hrs/set-font-size ()
    "Set the font to `hrs/default-font' at `hrs/current-font-size'."
    (set-frame-font
     (concat hrs/default-font "-" (number-to-string hrs/current-font-size))))

  (defun hrs/reset-font-size ()
    "Change font size back to `hrs/default-font-size'."
    (interactive)
    (setq hrs/current-font-size hrs/default-font-size)
    (hrs/set-font-size))

  (defun hrs/increase-font-size ()
    "Increase current font size by a factor of `hrs/font-change-increment'."
    (interactive)
    (setq hrs/current-font-size
          (ceiling (* hrs/current-font-size hrs/font-change-increment)))
    (hrs/set-font-size))

  (defun hrs/decrease-font-size ()
    "Decrease current font size by a factor of `hrs/font-change-increment', down to a minimum size of 1."
    (interactive)
    (setq hrs/current-font-size
          (max 1
               (floor (/ hrs/current-font-size hrs/font-change-increment))))
    (hrs/set-font-size))

  (define-key global-map (kbd "C-)") 'hrs/reset-font-size)
  (define-key global-map (kbd "C-+") 'hrs/increase-font-size)
  (define-key global-map (kbd "C-=") 'hrs/increase-font-size)
  (define-key global-map (kbd "C-_") 'hrs/decrease-font-size)
  (define-key global-map (kbd "C--") 'hrs/decrease-font-size)
  (hrs/reset-font-size)
#+END_SRC

** Modeline

=moody= gives a truly lovely ribbon-based modeline.

#+begin_src emacs-lisp
  (use-package moody
    :config
    (setq x-underline-at-descent-line t)
    (moody-replace-mode-line-buffer-identification)
    (moody-replace-vc-mode))
#+end_src

#+RESULTS:
: t

#+begin_src emacs-lisp
  ;; Set visual-line-mode and change auto-fill-mode to " AF".
  (use-package emacs
    :delight
    (auto-fill-function ".⏎")
    (visual-line-mode " VLM")
    :custom
    (find-file-run-dired nil)
    )
#+end_src

#+RESULTS:

** =avy=
Years ago, I tried avy, and liked it, but it didn't get into my normal
pattern. [[https://karthinks.com/software/avy-can-do-anything/][Avy can do anything]], is an article that lays avy out in more detail.
The ~avy-setup-default~ binds ~C-'~ to avy-search in isearch.

#+begin_src emacs-lisp
  (use-package avy
    :ensure t
;;    :config
;;    (avy-setup-default)
    :custom
    (avy-timeout-seconds 0.5)
    :bind
    (
     ;;("C-c C-j" . avy-resume)
     ("C-;" . avy-resume)
     ("C-:" . avy-goto-char-timer)
     :map isearch-mode-map
     ("C-'" . avy-search)

     )
    )
#+end_src

#+RESULTS:
: avy-search

** [[ https://github.com/minad/vertico][Vertico]]

Vertico is a low level completion component.  Basically, this just gives you
better access to potential matches in your search buffer, in a vertical Vfashion.


#+BEGIN_SRC emacs-lisp
(use-package vertico
  :init
  (vertico-mode)

  ;; Different scroll margin
  ;; (setq vertico-scroll-margin 0)

  ;; Show more candidates
  ;; (setq vertico-count 20)

  ;; Grow and shrink the Vertico minibuffer
  ;; (setq vertico-resize t)

  ;; Optionally enable cycling for `vertico-next' and `vertico-previous'.
  ;; (setq vertico-cycle t)
  )

#+END_SRC

#+RESULTS:

** =orderless=

#+begin_src emacs-lisp
  (use-package orderless
    :ensure t
    :custom
    (completion-styles '(orderless basic))
    (completion-category-overrides '((file (styles basic partial-completion))))
    )
#+end_src

#+RESULTS:

** =consult=

#+begin_src emacs-lisp
;; Example configuration for Consult
(use-package consult
  ;; Replace bindings. Lazily loaded due by `use-package'.
  :bind (;; C-c bindings (mode-specific-map)
         ("C-c h" . consult-history)
         ("C-c m" . consult-mode-command)
         ("C-c k" . consult-kmacro)
         ;; C-x bindings (ctl-x-map)
         ("C-x M-:" . consult-complex-command)     ;; orig. repeat-complex-command
         ("C-x b" . consult-buffer)                ;; orig. switch-to-buffer
         ("C-x 4 b" . consult-buffer-other-window) ;; orig. switch-to-buffer-other-window
         ("C-x 5 b" . consult-buffer-other-frame)  ;; orig. switch-to-buffer-other-frame
         ("C-x r b" . consult-bookmark)            ;; orig. bookmark-jump
         ("C-x p b" . consult-project-buffer)      ;; orig. project-switch-to-buffer
         ;; Custom M-# bindings for fast register access
         ("M-#" . consult-register-load)
         ("M-'" . consult-register-store)          ;; orig. abbrev-prefix-mark (unrelated)
         ("C-M-#" . consult-register)
         ;; Other custom bindings
         ("M-y" . consult-yank-pop)                ;; orig. yank-pop
         ("<help> a" . consult-apropos)            ;; orig. apropos-command
         ;; M-g bindings (goto-map)
         ("M-g e" . consult-compile-error)
         ("M-g f" . consult-flymake)               ;; Alternative: consult-flycheck
         ("M-g g" . consult-goto-line)             ;; orig. goto-line
         ("M-g M-g" . consult-goto-line)           ;; orig. goto-line
         ("M-g o" . consult-outline)               ;; Alternative: consult-org-heading
         ("M-g m" . consult-mark)
         ("M-g k" . consult-global-mark)
         ("M-g i" . consult-imenu)
         ("M-g I" . consult-imenu-multi)
         ;; M-s bindings (search-map)
         ("M-s d" . consult-find)
         ("M-s D" . consult-locate)
         ("M-s g" . consult-greprep)
         ("M-s r" . consult-ripgrep)
         ("M-s l" . consult-line)
         ("M-s L" . consult-line-multi)
         ("M-s m" . consult-multi-occur)
         ("M-s k" . consult-keep-lines)
         ("M-s u" . consult-focus-lines)
         ;; Isearch integration
         ("M-s e" . consult-isearch-history)
         :map isearch-mode-map
         ("M-e" . consult-isearch-history)         ;; orig. isearch-edit-string
         ("M-s e" . consult-isearch-history)       ;; orig. isearch-edit-string
         ("M-s l" . consult-line)                  ;; needed by consult-line to detect isearch
         ("M-s L" . consult-line-multi)            ;; needed by consult-line to detect isearch
         ;; Minibuffer history
         :map minibuffer-local-map
         ("M-s" . consult-history)                 ;; orig. next-matching-history-element
         ("M-r" . consult-history))                ;; orig. previous-matching-history-element

  ;; Enable automatic preview at point in the *Completions* buffer. This is
  ;; relevant when you use the default completion UI.
  :hook (completion-list-mode . consult-preview-at-point-mode)

  ;; The :init configuration is always executed (Not lazy)
  :init

  ;; Optionally configure the register formatting. This improves the register
  ;; preview for `consult-register', `consult-register-load',
  ;; `consult-register-store' and the Emacs built-ins.
  (setq register-preview-delay 0.5
        register-preview-function #'consult-register-format)

  ;; Optionally tweak the register preview window.
  ;; This adds thin lines, sorting and hides the mode line of the window.
  (advice-add #'register-preview :override #'consult-register-window)

  ;; Use Consult to select xref locations with preview
  (setq xref-show-xrefs-function #'consult-xref
        xref-show-definitions-function #'consult-xref)

  ;; Configure other variables and modes in the :config section,
  ;; after lazily loading the package.
  :config

  ;; Optionally configure preview. The default value
  ;; is 'any, such that any key triggers the preview.
  ;; (setq consult-preview-key 'any)
  ;; (setq consult-preview-key (kbd "M-."))
  ;; (setq consult-preview-key (list (kbd "<S-down>") (kbd "<S-up>")))
  ;; For some commands and buffer sources it is useful to configure the
  ;; :preview-key on a per-command basis using the `consult-customize' macro.
  (consult-customize
   consult-theme
   :preview-key '(:debounce 0.2 any)
   consult-ripgrep consult-git-grep consult-grep
   consult-bookmark consult-recent-file consult-xref
   consult--source-bookmark consult--source-recent-file
   consult--source-project-recent-file
   :preview-key (kbd "M-."))

  ;; Optionally configure the narrowing key.
  ;; Both < and C-+ work reasonably well.
  (setq consult-narrow-key "<") ;; (kbd "C-+")

  ;; Optionally make narrowing help available in the minibuffer.
  ;; You may want to use `embark-prefix-help-command' or which-key instead.
  ;; (define-key consult-narrow-map (vconcat consult-narrow-key "?") #'consult-narrow-help)

  ;; By default `consult-project-function' uses `project-root' from project.el.
  ;; Optionally configure a different project root function.
  ;; There are multiple reasonable alternatives to chose from.
  ;;;; 1. project.el (the default)
  ;; (setq consult-project-function #'consult--default-project--function)
  ;;;; 2. projectile.el (projectile-project-root)
  ;; (autoload 'projectile-project-root "projectile")
  ;; (setq consult-project-function (lambda (_) (projectile-project-root)))
  ;;;; 3. vc.el (vc-root-dir)
  ;; (setq consult-project-function (lambda (_) (vc-root-dir)))
  ;;;; 4. locate-dominating-file
  ;; (setq consult-project-function (lambda (_) (locate-dominating-file "." ".git")))
)
#+end_src

#+RESULTS:
: consult-history

** =dired=

You can use ~use-package~ t configure parts of emacs as well.  One thing, you
can do is alter your ~ls~ listing for your favorite look in ~dired~.

#+begin_src emacs-lisp
  (use-package dired
    :ensure nil
    :custom
    (dired-listing-switches "-aBhl --group-directories-first"))
#+end_src

** Wrap paragraphs automatically

=AutoFillMode= automatically wraps paragraphs, kinda like hitting =M-q=. I wrap
a lot of paragraphs, so this automatically wraps 'em when I'm writing text,
Markdown, or Org.

#+BEGIN_SRC emacs-lisp
  (add-hook 'text-mode-hook 'turn-on-auto-fill)
  (add-hook 'gfm-mode-hook 'turn-on-auto-fill)
  (add-hook 'org-mode-hook 'turn-on-auto-fill)
#+END_SRC

Sometimes, though, I don't wanna wrap text. This toggles wrapping with =C-c q=:

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-c q") 'auto-fill-mode)
#+END_SRC

* Org Mode

Custom vars: use a UTF-8 arrow, instead of the usual ellipsis (=...=) that org
displays when there's stuff under a header; Use [[https://orgmode.org/worg/org-contrib/babel/examples/fontify-src-code-blocks.html][syntax highlighting in source
blocks]] while editing; Make TAB act as if it were issued in a buffer of the
language's major mode; When editing a code snippet, use the current window
rather than popping open a new one [[https://orgmode.org/manual/Editing-Source-Code.html][Editing Source Code]].

#+begin_src emacs-lisp
  (use-package org
                  :mode (( "\\.org$" . org-mode ))
                  :ensure org
                  :custom
                    (org-ellipsis "⤵")
                    (org-src-fontify-natively t)
                    (org-src-tab-acts-natively t)
                    (org-src-window-setup 'current-window)
                    (org-datetree-add-timestamp 'active)
                    (org-default-notes-file "~/org/notes")
  )
  (use-package org-capture
    :ensure nil
    :after org

    )

#+end_src

#+RESULTS:



I'd like the initial scratch buffer to be in Org:
#+begin_src emacs-lisp
  (setq initial-major-mode 'org-mode)
#+end_src

** Look and Feel

This adds nice UTF-8 bullets to outlines

#+begin_src emacs-lisp
  (use-package org-bullets
    :init
    (add-hook 'org-mode-hook 'org-bullets-mode))
#+end_src


** Capture templates



** Note Taking

I'm looking into the best method for note taking.  Originally, I was going
directly to =org-roam=, and while I do like the literature notes, I think it's
worth looking at the original emacs tools as well.  [[https://orgmode.org/manual/Agenda-Views.html][Agendas]] are a standard part
of =org-mode= and can be used to find =TODOS= etc. You can extend the standard
journals with [[https://github.com/bastibe/org-journal][=org-journal=]] which splits days up a bit more, maybe useful for
sharing via git.  Two explainations of useing these are
[[http://cachestocaches.com/2016/9/my-workflow-org-agenda/]],
[[http://www.howardism.org/Technical/Emacs/journaling-org.html]] and
[[http://doc.norang.ca/org-mode.html]].  There are some complaints that
=org-journal= is too slow for searching, and more judicious use of =org-capture=
might also work.  They seem a bit more complex, though, so I might just start w/
=org-journal=.

** =org-journal=

   Okay, our original plan will be to integrate org-journal into my framework,
   but using =org-capture= templates as the methodology to do this.  I'm
   currently expecting that

   #+begin_src emacs-lisp
     (use-package org-journal
     :ensure t
     :defer t
     :init
     ;; Change default prefix key; needs to be set before loading org-journal
     (setq org-journal-prefix-key "C-c j ")
     (defun org-journal-find-location ()
       ;; Open today's journal, but specify a non-nil prefix argument in order to
       ;; inhibit inserting the heading; org-capture will insert the heading.
       (org-journal-new-entry t)
       (unless (eq org-journal-file-type 'daily)
         (org-narrow-to-subtree))
       (goto-char (point-max)))
      (add-to-list 'org-capture-templates
                  '("j" "Journal entry" plain (function org-journal-find-location)
                    "** %(format-time-string org-journal-time-format)%^{Title}\n%i%?"
                    :jump-to-captured t :immediate-finish t))

     :config
     (setq org-journal-dir "~/org/journal/"
           org-journal-date-format "%A, %d %B %Y"))


   #+end_src

   #+RESULTS:

** =org-roam=

*** citations

Once we have our citations in org-roam, [[https://github.com/org-roam/org-roam-bibtex][org-roam-bibtext]] shows how to
incorporate into your paper writing utilities.  [[https://github.com/bdarcus/citar][Citar]] shows selection of
citations, then.

* TODO [[https://docs.projectile.mx/projectile/installation.html][=Projectile=]]


* Programming

** Magit

There are some suggestions from [[https://github.com/hrs/dotfiles/blob/main/emacs/.config/emacs/configuration.org#magit][HRS]], [[https://github.com/bradwright/emacs-d/blob/master/packages/init-magit.el][bradwright]], about good ways to use this, although I'm
not sure about the

#+begin_src emacs-lisp
    (use-package magit
    :bind (("C-c g" . magit-status)
           ("C-c C-g l" . magit-file-log)
           ("C-c f" . magit-grep))

    :config
  ;  (use-package git-commit)
  ;  (use-package magit-section)
  ;  (use-package with-editor)

    (setq magit-push-always-verify nil
          git-commit-summary-max-length 50))

#+end_src

** TODO LSP-mode
There are two competing Language server implementations, =lsp-mode= and
[[https://github.com/joaotavora/eglot][=elgot=]].  Of the two =elgot= seems more simple.  So simple, I don't even know
where the servers live :)

** SQL Mode
   We can use the standard sql-mode, but we really need to add in the sql-indent
   minor mode, esp for source code blocks.
   #+begin_src emacs-lisp
     (use-package sql-indent
       :after sql
       )
   #+end_src

** Sparql / TTL /TRIG

#+BEGIN_SRC emacs-lisp
(use-package sparql-mode
:mode (( "\\.sparql$" . sparql-mode )
( "\\.ru$" . sparql-mode )
( "\\.rq$" . sparql-mode )
)
:ensure sparql-mode
)
#+END_SRC

** JSON

Although, I don't use it often, =jq-mode= can be useful for longish jq
scripts. More importantly, it can be used with =babel= for literate programming.

#+BEGIN_SRC emacs-lisp
(use-package jq-mode
:mode (("\\.jq$" . jq-mode))
)
#+END_SRC

* Babel
Second only to org-mode, =babel= is the greatest component for literate
programming, it allows for multiple languages to be added to SRC blocks.

#+begin_src emacs-lisp
  ;;  (use-package gnuplot)
    (use-package ob-http)

    (org-babel-do-load-languages
     'org-babel-load-languages
     '((emacs-lisp . t)
       (sql . t)
       (shell . t)
       (ruby . t)
       (dot . t)
       (jq  . t)
       (sparql . t)
       (http . t)
       (js . t)
       (eshell . t)
   ;;    (gnuplot . t)
   ))

#+end_src

#+RESULTS:

Don't ask before evaluating code blocks.

#+begin_src emacs-lisp
  (setq org-confirm-babel-evaluate nil)
#+end_src

#+RESULTS:

Translate regular ol' straight quotes to typographically-correct curly quotes
when exporting.

#+begin_src emacs-lisp
  (setq org-export-with-smart-quotes t)
#+end_src


** UML
UML diagrams is not super well supported currently, but you can
#+BEGIN_SRC emacs-lisp
  (setq org-plantuml-jar-path
        (expand-file-name "/usr/share/plantuml/plantuml.jar"))
  (add-to-list 'org-src-lang-modes '("uml" . plantuml))
#+END_SRC

** =htmlize=
Use =htmlize= to ensure that exported code blocks use syntax highlighting.

#+begin_src emacs-lisp
  (use-package htmlize)
#+end_src

** =dot=
Associate the "dot" language with the =graphviz-dot= major mode.

#+begin_src emacs-lisp
  (use-package graphviz-dot-mode)
  (add-to-list 'org-src-lang-modes '("dot" . graphviz-dot))
#+end_src
